/*
 * Copyright (c) 2020 Armel Soro
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
buildscript {
    ext {
        kotlinVersion = '2.0.10'
        springBootVersion = '3.3.2'
        jacksonVersion = '2.17.2'
        graphqlSpringBootStarterVersion = '11.1.0'
    }
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath "io.github.gradle-nexus:publish-plugin:2.0.0"
        classpath "pl.allegro.tech.build:axion-release-plugin:1.18.4"
    }
}

apply plugin: "io.github.gradle-nexus.publish-plugin"
apply plugin: "pl.allegro.tech.build.axion-release"

scmVersion {
    localOnly = true
    tag {
        prefix = ''
    }
    snapshotCreator { version, position -> '' }
    hooks {
        pre 'fileUpdate', [
                files: [
                        'README.md',
                        'Dockerfile',
                        'Procfile'
                ],
                pattern: {v,p -> /service-names-port-numbers-app-$v/},
                replacement: {v, p -> "service-names-port-numbers-app-$v"}]
        pre 'fileUpdate', [
                files: [
                        'README.md'
                ],
                pattern: {v,p -> /<version>$v/},
                replacement: {v, p -> "<version>$v"}]
        pre 'fileUpdate', [
                files: [
                        'README.md'
                ],
                pattern: {v,p -> /service-names-port-numbers-library\:$v/},
                replacement: {v, p -> "service-names-port-numbers-library:$v"}]
        pre 'fileUpdate', [
                files: [
                        'library/build.gradle'
                ],
                pattern: {v,p -> /version '$v'/},
                replacement: {v, p -> "version '$v'"}]
        pre 'commit'
    }
}

group = "org.rm3l"

allprojects {
    buildscript {
        repositories {
            maven {
                url "https://plugins.gradle.org/m2/"
            }
            mavenCentral()
            maven { url 'https://repo.spring.io/plugins-release' }
        }
    }
    project.version = scmVersion.version
}

subprojects {
    apply plugin: 'kotlin'

    group = 'org.rm3l'
    version = rootProject.scmVersion.version

    sourceCompatibility = 11
    targetCompatibility = 11
    compileKotlin {
        kotlinOptions.jvmTarget = "11"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "11"
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation(platform('org.junit:junit-bom:5.11.0'))
        testImplementation('org.junit.jupiter:junit-jupiter')
    }

}

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
} else {
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
}


nexusPublishing {
    // packageGroup = "org.rm3l"  //defaults to 'project.group'
    repositories {
        sonatype { 
            stagingProfileId = project.ext.has('sonatypeStagingProfileId') ? project.ext.sonatypeStagingProfileId : ''
            username = project.ext.has('ossrhUsername') ? project.ext.ossrhUsername : ''
            password = project.ext.has('ossrhPassword') ? project.ext.ossrhPassword : ''
        }
    }
}
